# ###################################################################
# Mp3tag (v2.36 or higher) parsing for VGMdb.net, created by dano on 2010-01-17
#
# v5 from 2019-05-28
# Uses the release url to find an album
#
# Copy this file to your sources directory at
# %APPDATA%\Mp3tag\data\sources
#
# Changelog:
# [2015-09-01] FIX: Coverurl fixed (by PBX_g33k)
# 
# ###################################################################

[Name]=VGMdb
[BasedOn]=VGMdb.net
[SearchBy]=%VGMdb Album Url%

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################

# Album
outputto "Album"
findline "class=\"albumtitle"
replace "|" "$verticalBar()"
findinline "class=\"albumtitle"
findinline ">"
sayuntil "</span"

# Coverurl
outputto "coverurl"
findline "id=\"coverart\""
findinline "style=\"background-image: url("
if "'"
	movechar 1
	sayuntil "'"
else
	findinline "../"
	sayuntil "\""
endif

# set "coverurl"


# Catalog
outputto "Catalog"
findline "<b>Catalog Number</b>"
joinuntil "</tr>"
findinline "<td width=\"100%\">"
ifnot "N/A"
	if "<span id="
		findinline "#\">"
		sayuntil "</a>"
	else
		regexpreplace "<a[^>]+>" ""
		replace "</a>" ""
		findinline "<td width=\"100%\">"
		sayuntil "</td>"
	endif
endif

# Release Date
outputto "Year"
findline "<b>Release Date</b>"
moveline 1
sayregexp "(?<=<td>)\d{4} *(?=</td>)" "" 

if "<td><a "
	findinline "title=\"View albums released on"
	sayregexp "\d{4}(?=\" href)" "" "</a>"
endif


# Publish Format
outputto "Format"
findline "<b>Publish Format</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"

# Media Format
outputto "Mediatype"
findline "<b>Media Format</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"

# Classification (Genre)
outputto "Genre"
findline "<b>Classification</b>"
joinuntil "</tr>"
findinline "<td>"
sayuntil "</td>"


# Publisher
findline "<b>Published by</b>"	
outputto "Publisher"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<span class=\"productname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span class=\"productname\" lang=\"[-\w]+\" style=\"display:none\">([^<]+)</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""

# Composer
findline "<b>Composed by</b>"
outputto "Composer"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""


# Arranged
findline "<b>Arranged by</b>"
outputto "Writer"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""


# Performed
findline "<b>Performed by</b>" 1 1
unspace
if "<tr><td"
outputto "Artist"
moveline 1
regexpreplace "<span style=\"display:none\"><em> */ *</em></span>" ""
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"en\" style=\"display:inline\">([^<]+)</span>" "$1"
regexpreplace "<span title=\"[^\"]*\" class=\"artistname\" lang=\"ja\" style=\"display:none\">.+?</span>" ""
regexpreplace "<a[^>]+>" ""
replace "</a>" ""
sayregexp "(?<=<td>)[^<]+(?=</td>)" ""
else
	findline "nff44455553333332_____-------" -1 1
endif

# Tracks
# findline "<b>Disc 2</b></span>"
findline "class=\"smallfont\"><span class=\"label\">"
do
	outputto "Tracks"
	moveline 1
	findinline "width=\"100%\">"
	sayuntil "</td>"
	say "|"
	
	outputto "_Length"
	moveline 1
	sayregexp "(?<=<span class=\"time\">)[\d:]+(?=</span>)" ""
	say "|"

	findline "class=\"smallfont\"><span class=\"label\">" 1 1	
while "<td "

# Notes
gotoline 1
outputto "Notes"
findline "<h3>Notes</h3>"
findline "class=\"smallfont\">"
regexpreplace "\s*<br ?\/?>\s*" "\r\n"
findinline "class=\"smallfont\">"
ifnot "<i class='label'>"
	sayuntil "</div>"
endif

# VGMdb Album URL
outputto "VGMdb Album Url"
sayoutput "CurrentUrl"

# ... Customization ...

#set "genre" "Game"

# Fields that you want to remove
#	(to remove a field write: set "field")
